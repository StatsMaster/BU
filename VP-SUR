
/* clean */

data VP_v1 ;
 set VP_2014;

  /* Reduction in DIA BPS from Rate Review */
 DIA_BPS_PC  = (proposed_dia_bps_pct  - dia_bps_pct) /dia_bps_pct ;

 DIAO_BPS_PC = (proposed_diao_bps_pct - diao_bps_pct)/diao_bps_pct;

 if DIA_BPS_PC  < 0 and DIA_BPS_PC ne . ;
 if dia_difference < 0 and dia_difference ne . ; 
 if compress(rr_reason_code) = 'RatesComparable-NoChange' then delete;
 if compress(rr_rep) in 
 ('ExzelliaDumas', 'JakaraMapp', 'CassandraPerry','Danielle Richard') then delete;
 if year(saved_date) = 2014 ;
 SAVE_MONTH = month(saved_date) ;

 DIAO_DIFF_to_VOL = diao_difference / vol;

 		if pricing_method in ('Pass Through (ICDIF)', 'Pass Through (ICPLS)') then P_Method = 'Pass Through';
 else	if pricing_method in ('Tiered') then P_Method = 'Tiered';
 else	P_Method = 'Other';


run;

/* explore */

proc freq data = vp_v1;
	tables rr_reason_code rr_rep pricing_method ;
run;

proc freq data = vp_v1;
	tables Pricing_Method*P_Method P_Method / list missing ;
run;

proc univariate data = vp_v1;
	var DIA_BPS_PC ;
run ;

/* Remove duplicates */

proc sort data = VP_v1 out = vp_v2 NoDupKey ;
	by MID ;
run ;

proc univariate data = vp_v2;
	var DIA_BPS_PC ;
run ;


/* Current MID Status */

proc sql;
	connect to oracle (user = &U. password = &P. path= 'NEWDW01.WORLD');
	create table TABLE_C as select * from connection to oracle ( select  distinct
	c.mid , 
	c.dbaname , 
	c.merchstatus_ecp , 
	c.date_opened , 
	c.date_closed ,
	c.first_deposit_date ,
	c.last_deposit_date 
	from 	browser.merch_demo_info_master 	c );
	disconnect from oracle;
quit;

proc sql;
	create table vp_v3 as select a.*,
	b.merchstatus_ecp , 
	datepart(b.date_opened) 		as OPD Format = Date9. , 
	datepart(b.first_deposit_date) 	as FDD Format = Date9. ,
	datepart(b.last_deposit_date)	as LDD Format = Date9. ,
	datepart(b.date_closed) 		as CLD Format = Date9.
	from vp_v2 a left join table_c b on a.mid = b.mid;
quit;

data vp_v4 ;
 set vp_v3 ;
 if OPD = . then DELETE;

  /* Survival Analysis Variables SAV */

 * 1.) Does the duration indicate cencoring? ;
 if merchstatus_ecp in ('C','D') then STTS = 1 ; else STTS = 0 ;

 * 2.) What is the MIDs duration from the saved date? ;
 		if STTS = 0 then  DUR = intck('month'	, Saved_Date , '17FEB2015'D);
 else 	if STTS = 1 then  DUR = intck('month'	, Saved_Date , LDD);

 		if STTS = 0 then  DUR_DAY = intck('day'	, Saved_Date , '17FEB2015'D);
 else 	if STTS = 1 then  DUR_DAY = intck('day'	, Saved_Date , LDD);

 		if vol <=  6788.07 then VOL_GRP = 1;
 else	if vol <= 43704.61 then VOL_GRP = 2;
 else							VOL_GRP = 3;

run ;

/*
proc univariate data = vp_v4 ;
	var vol ; histogram ;
run ;

proc freq data = vp_v4 ;
	tables VOL_GRP ;
run ;
*/

proc rank data = vp_v4 groups = 3 out = vp_v4 ; 
	var DIAO_BPS_PC ; 
	ranks bin ; 
run ;

proc means data = vp_v4 median;
 class bin ;
 var DIAO_BPS_PC ;
 ods output Summary = BIN_MED;
run ;

data vp_v4;
 set vp_v4;
 VOL_GRP_BIN = compress("Vol"||VOL_GRP||"+ BIN"||BIN);
run;

/* Overall Survival */

Title "Overall Survival Months";
proc lifetest 
	data 	= vp_v4
	outsurv = outsurv
	method  = Life Plot = (S, H) Width = 1 Graphics;
	Time Dur*STTS(0);
run;
title;
/*
Title "Overall Survival Months Days";
proc lifetest 
	data 	= vp_v4
	outsurv = outsurv
	method  = Life Plot = (S, H) Width = 1 Graphics;
	Time Dur_Day*STTS(0);
run;
title;

ods output ProductLimitEstimates = ple;
proc lifetest data=vp_v4 nelson outs=outvp_v4 ;
time Dur*STTS(0);
run;

proc sgplot data = ple;
series x = dur y = CumHaz ;
run;
*/

/* Survival by Pricing Method */

Title "Overall Survival by Pricing Method";
proc lifetest 
	data 	= vp_v4
	outsurv = outsurv
	method  = Life Plot = (S, H) Width = 1 Graphics;
	strata P_Method ;
	Time Dur*STTS(0);
run;
title;

/* Survival by VOL (BIN) */

Title "Overall Survival by VOL_GRP_BIN";
proc lifetest 
	data 	= vp_v4
	outsurv = outsurv
	method  = Life Plot = (S, H) Width = 1 Graphics;
	strata VOL_GRP_BIN ;
	Time Dur*STTS(0);
run;
title;

/* Survival by Pricing Method, Pass Through -- DIAO_BPS_PC in BINs */
Title "Overall Survival by Pass Through, DIAO BPS PC BINs";
proc lifetest 
	data 	= vp_v4 (where= (P_Method = 'Pass Through'))
	outsurv = outsurv
	method  = Life Plot = (S, H) Width = 1 Graphics;
	strata BIN ;
	Time Dur*STTS(0);
run;
title;

/* Survival by Pricing Method, Tiered -- DIAO_BPS_PC in BINs */
Title "Overall Survival by Tiered, DIAO BPS PC BINs";
proc lifetest 
	data 	= vp_v4 (where= (P_Method = 'Tiered'))
	outsurv = outsurv
	method  = Life Plot = (S, H) Width = 1 Graphics;
	strata BIN ;
	Time Dur*STTS(0);
run;
title;

/* Survival  */

Title "Overall Survival by RateAdj Strata";
proc lifetest 
	data 	= vp_v4
	outsurv = outsurv
	method  = Life Plot = (S, H) Width = 1 Graphics;
	strata bin;
	Time Dur*STTS(0);
run;
title;

Title "Model the SUR by  the RateAdj";
proc lifereg data = vp_v4 outest = A ;
	model DUR*STTS(0) = BIN BIN*BIN
	/ dist = LNORMAL ;
	output out = B xbeta = LP ;
run ;
Title;

/* Rep Impact */

Title "Overall Survival by RR-Rep";
proc lifetest 
	data 	= vp_v4 
	outsurv = outsurv
	method  = Life Plot = (S, H) Width = 1 Graphics;
	strata rr_rep ;
	Time Dur*STTS(0);
run;
title;

Title "Overall Survival by Top RR-Rep";
proc lifetest 
	data 	= vp_v4  
	(where=(rr_rep in ('Erika Morris','LaVonne Williams','April Alexander','Latoya Wright','Erica Atkins')))
	outsurv = outsurv
	method  = Life Plot = (S, H) Width = 1 Graphics;
	strata rr_rep ;
	Time Dur*STTS(0);
run;
title;

/* Pricing Program Impact */

Title "Overall Survival by Pricing Method";
proc lifetest 
	data 	= vp_v4 
	outsurv = outsurv
	method  = Life Plot = (S, H) Width = 1 Graphics;
	strata pricing_method ;
	Time Dur*STTS(0);
run;
title;

/* DIAO Dollar Difference */

Title "Overall Survival by diao_difference_pct";
proc lifetest 
	data 	= vp_v4
	outsurv = outsurv
	method  = Life Plot = (S, H) Width = 1 Graphics;
	strata DIAO_DIFF_to_VOL ;
	Time Dur*STTS(0);
run;
title;

/* DIAO Dollar Difference */

Title "Overall Survival by diao_difference over vol";
proc lifetest 
	data 	= vp_v4
	outsurv = outsurv
	method  = Life Plot = (S, H) Width = 1 Graphics;
	strata BIN ;
	Time Dur*STTS(0);
run;
title;

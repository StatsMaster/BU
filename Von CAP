

/* 

   _____              _____  
  / ____|     /\     |  __ \ 
 | |         /  \    | |__) |
 | |        / /\ \   |  ___/ 
 | |____ _ / ____ \ _| |_    
  \_____(_)_/    \_(_)_(_)   
                             
                                                 
   _____          _                                           _   _            _   _               _____                                     
  / ____|        | |                                /\       | | (_)          | | (_)             |  __ \                                    
 | |    _   _ ___| |_ ___  _ __ ___   ___ _ __     /  \   ___| |_ ___   ____ _| |_ _  ___  _ __   | |__) | __ ___   __ _ _ __ __ _ _ __ ___  
 | |   | | | / __| __/ _ \| '_ ` _ \ / _ \ '__|   / /\ \ / __| __| \ \ / / _` | __| |/ _ \| '_ \  |  ___/ '__/ _ \ / _` | '__/ _` | '_ ` _ \ 
 | |___| |_| \__ \ || (_) | | | | | |  __/ |     / ____ \ (__| |_| |\ V / (_| | |_| | (_) | | | | | |   | | | (_) | (_| | | | (_| | | | | | |
  \_____\__,_|___/\__\___/|_| |_| |_|\___|_|    /_/    \_\___|\__|_| \_/ \__,_|\__|_|\___/|_| |_| |_|   |_|  \___/ \__, |_|  \__,_|_| |_| |_|
                                                                                                                    __/ |                    
                                                                                                                   |___/                     
*/


/*
---Update May 19, 2014 --- want 2 full months of being open; 
therefore look at MIDs opened in Feb 2014 (then have all of March & Apr 2014 to open) -----------
*/

/* Macro Properties */

/*
	Open Period - Two months prior to this month.
	Two months prior to now is selected to allow opened MID two months to activate

	Open Period - First day of the month

	Open Period - Last day of the month

*/

%let Today = %sysfunc(today(),Date9.) ; %Put Today is &Today;

proc sql;
	connect to oracle (user = &U. password = &P. path= 'NEWDW01.WORLD') ;
	create table Dates as select * from connection to oracle ( Select 
	mid, datevalue as Signed_Date from DW.DATES  where datetype = 'SGN') ;
	create table REPS as select * from connection to oracle ( Select 
	REPCODE, REPNAME, Channel as REP_Channel from BSNSDEVL.REPINFO ) ;
	disconnect from oracle ;
quit;

%Macro CAP;

%do x = 3 %to 3;

%Global Open_P ;

data _null_ ;
	Call Symputx
	('OP_FD',
	Upcase(Compress("'"||put(intnx('month',"&Today."D,-&x.,'B'),date9.)||"'"))) ; 
	Call Symputx
	('OP_LD',
	Upcase(Compress("'"||put(intnx('month',"&Today."D,-&x.,'E'),date9.)||"'"))) ; 
if month(intnx('month',"&Today."D,-&x.)) < 10 then do;
	Call Symputx
	('Open_Period',
	Upcase(Compress("'"||year(intnx('month',"&Today."D,-&x.))||"0"||month(intnx('month',"&Today."D,-&x.))||"'"))) ;
	Call Symputx
	('Open_P',
	Upcase(Compress(year(intnx('month',"&Today."D,-&x.))||"0"||month(intnx('month',"&Today."D,-&x.)))));
end;
else do;
	Call Symputx
	('Open_Period',
	Upcase(Compress("'"||year(intnx('month',"&Today."D,-&x.))||month(intnx('month',"&Today."D,-&x.))||"'"))) ; 
	Call Symputx
	('Open_P',
	Upcase(Compress(year(intnx('month',"&Today."D,-&x.))||month(intnx('month',"&Today."D,-&x.))))) ;
 end;
run ;

%Put 
The Open Period is = &Open_Period
The Open P is = &Open_P
The first date of the period is = &OP_FD.
The last  date of the period is = &OP_LD.
;


data _null_ ;
	Call Symputx
	('OP_FD',
	Upcase(Compress("'"||put(intnx('month',"&Today."D,-&x.,'B'),date9.)||"'"))) ; 
	Call Symputx
	('OP_LD',
	Upcase(Compress("'"||put(intnx('month',"&Today."D,-&x.,'E'),date9.)||"'"))) ; 
if month(intnx('month',"&Today."D,-&x.)) < 10 then do;
	Call Symputx
	('Open_Period',
	Upcase(Compress("'"||year(intnx('month',"&Today."D,-&x.))||"0"||month(intnx('month',"&Today."D,-&x.))||"'"))) ;
	Call Symputx
	('Open_P',
	Upcase(Compress(year(intnx('month',"&Today."D,-&x.))||"0"||month(intnx('month',"&Today."D,-&x.)))));
end;
else do;
	Call Symputx
	('Open_Period',
	Upcase(Compress("'"||year(intnx('month',"&Today."D,-&x.))||month(intnx('month',"&Today."D,-&x.))||"'"))) ; 
	Call Symputx
	('Open_P',
	Upcase(Compress(year(intnx('month',"&Today."D,-&x.))||month(intnx('month',"&Today."D,-&x.))))) ;
 end;
run ;

%Put 
The Open Period is = &Open_Period
The Open P is = &Open_P
The first date of the period is = &OP_FD.
The last  date of the period is = &OP_LD.
;

proc sql;
	connect to oracle (user = &U. password = &P. path= 'NEWDW01.WORLD') ;
	create table CAP_&Open_P._v1 as select * from connection to oracle (select 
	&Open_Period. as OPN_PERIOD , 
	a.addressable , 
	a.channel , 
	a.rdc , 
	c.sponsorid , 
	c.assocnumber , 
	nvl(c.siccode,'9999') as SIC ,
	c.projmthvol		  as PMV ,
	c.mid , 
	c.dbaname , 
	c.merchstatus_ecp , 
	c.date_opened  			as OPN , 
	c.date_closed 			as CLO , 
	c.first_deposit_date 	as FDD , 
	c.last_deposit_date 	as LDD ,
	c.REPCODE ,
	coalesce(d.group_  ,'Null')  	as Rela_Type ,
	coalesce(d.rel_long,'Null')  	as Rela 
	from 		bsnsdevl.mid_owner 				a 
	inner join	browser.merch_demo_info_master 	c on  a.mid = c.mid
	left  join	bsnsdevl.mid_relationship 		d on  a.mid = d.mid
	where 	/*-- This includes Canada (sponsorids 0024 and 0025) */		
			c.sponsorid <  '0026'  
	and 	c.date_opened between &OP_FD. and &OP_LD. ) ;
	disconnect from oracle ;
quit;

PROC SQL ;
	connect to oracle (user = &U. password = &P. path= 'NEWDW01.WORLD');
	create table PType_CURR as select * from connection to oracle( 
	select Distinct  
	SPOEMID as MID ,
	PRICING_TYPE ,
	Channel 
	from RBAILEY.MODEL_2015 );
	disconnect from oracle ;
quit ;

PROC SQL ;
	connect to oracle (user = &U. password = &P. path= 'NEWDW01.WORLD');
	create table PType_OLD as select * from connection to oracle( 
	select Distinct  
	SPOEMID as MID 			,
	PRICING_TYPE  			,
	Channel 
	from RBAILEY.MODEL_APR13 );
	disconnect from oracle ;
quit ;

data PType ; set PType_CURR PType_OLD  ; run ;

proc sort data = PType NoDupKey; by MID; run;

proc sql;
	Create Table CAP_&Open_P._v2 as select a.*, b.signed_date, 
	c.REPNAME, c.REP_Channel
	From CAP_&Open_P._v1 a 
	left join dates 	 b on a.mid     = b.mid
	left join REPS  	 c on a.REPCODE = c.REPCODE ;
Quit;

/* Define the NonAct Buckets */

data  CAP_&Open_P._v3;
 set  CAP_&Open_P._v2;

 if   	sponsorid = '0006'
 or	  	SIC in ('6010','6011')
 then	Delete;

 /* 
 	Directs (Joe's) Groupings 
 		1.) Michael A. Lorino - KMS Client Groups
 		2.) Andy Popwell - BSS Sales (Atlanta Telesales)
 */	
 
 /* 1.) Michael A. Lorino - KMS Client Group  */
 if 	sponsorid = ('0035')
 then 	KMS = 1;  else KMS = 0;

 /* 2.) Andy Popwell - BSS Sales (Atlanta Telesales)  */
 if 	REP_Channel = 'ATL_TELESALES'
 then	BSS_Sales = 1; else BSS_Sales = 0;

 Format sign_to_FDD_time_bucket $20. First_Deposit_Date Sign_Date Date9.;
 First_Deposit_Date = datepart(FDD);
 Sign_Date 			= datepart(SIGNED_DATE);
 CLD				= datepart(CLO);
 /* Overall STP Metric */
 Days_Sign_to_FDD = (First_Deposit_Date - Sign_Date); 
 /* STP Buckets */
 if Days_Sign_to_FDD >= 0 then do;
 		if Days_Sign_to_FDD < 5  then do; sign_to_FDD_time_bucket = 'A  0 to  4 Days'; Sign_to_FDD_sort_flag = 1; end;
 else	if Days_Sign_to_FDD < 10 then do; sign_to_FDD_time_bucket = 'B  5 to  9 Days'; Sign_to_FDD_sort_flag = 2; end;
 else	if Days_Sign_to_FDD < 19 then do; sign_to_FDD_time_bucket = 'C 10 to 18 Days'; Sign_to_FDD_sort_flag = 3; end;
 else	if Days_Sign_to_FDD < 31 then do; sign_to_FDD_time_bucket = 'D 19 to 30 Days'; Sign_to_FDD_sort_flag = 4; end;
 else	if Days_Sign_to_FDD < 61 then do; sign_to_FDD_time_bucket = 'E 31 to 60 Days'; Sign_to_FDD_sort_flag = 5; end;
  else	if Days_Sign_to_FDD > 60 then do; sign_to_FDD_time_bucket = 'F 61 Plus Days' ; Sign_to_FDD_sort_flag = 6; end;
 end; 
 if sign_to_FDD_time_bucket = '' then do; 
	sign_to_FDD_time_bucket = 'G Not Activated Yet'; 
	Sign_to_FDD_sort_flag = 7; 
 end;

 /* Closed Account */
 if merchstatus_ecp in ('C','D') and CLD >= Sign_Date then Closed = 1; else Closed = 0;

 /* Potential Lost Processing Revenue to Late Activation */
 if PMV ne . then PAV = PMV*12; else PAV = .;

run;

Proc SQL;
	Create Table CAP_&Open_P._v3 as select  
	a.*, COALESCEC(b.PRICING_TYPE,'Null') as PRICING_TYPE
	From CAP_&Open_P._v3 a left join PType b on a.MID = b.MID;
Quit;

Proc Datasets lib=work NoList NoDetails ; 
	Delete CAP_&Open_P._v2 CAP_&Open_P._v1 ; 
Quit ;

%end;
%Mend CAP;
%CAP

%Put The Open Period is: &Open_P. ;

Proc Freq data = CAP_&Open_P._v3;
	tables sign_to_FDD_time_bucket ;
Run;

Proc Freq data = CAP_&Open_P._v3;
	tables sign_to_FDD_time_bucket*Closed / missing norow nopercent nocol nocum ;
Run;

Proc SQL;
	select count(*) as Total_MIDs_Opened  into: Total_MIDs_Opened  from CAP_&Open_P._v3 ;
	select count(*) as Total_MIDs_NonAct  into: Total_MIDs_NonAct  from CAP_&Open_P._v3 where sign_to_FDD_time_bucket = 'G Not Activated Yet';
	select count(*) as Total_MIDs_LateAct into: Total_MIDs_LateAct from CAP_&Open_P._v3 where sign_to_FDD_time_bucket = 'F 61 Plus Days';
	Select Distinct (&Total_MIDs_LateAct. + &Total_MIDs_NonAct.) as NonAct_CNT From CAP_&Open_P._v3;
	Select Distinct (&Total_MIDs_LateAct. + &Total_MIDs_NonAct.)/&Total_MIDs_Opened. as NonAct_Rate Format = Percent8.2 from CAP_&Open_P._v3;
Quit;

FileName test EMail 'Samuel.Seibert@Elavon.com' EMAILID ='Outlook' ;

DATA _null_ ;
	File test
	SUBJECT	= "CAP for &Open_P is Finished"
	TO		= 'Hokaten@gmail.com'
	CC		= 'Samuel.Seibert@Elavon.com'
	;
	Put "CAP for &Open_P is Finished";
	Put ' ';
RUN ;

%let CAP_IT_OFF = CAP_&Open_P._v3;

%Put How do you CAP it off? --> &CAP_IT_OFF ;


/* For the Non-Activation Stacked 100% Bar Charts

/* Data Table */
/*
Proc Freq data = CAP_&Open_P._v3;
	Where Channel not in ('OPAY','');
	Tables sign_to_FDD_time_bucket*Channel
	/  missing nopercent nocum norow nocol;
Run;
*/
/* Data Chart */
/*
Proc Freq data = CAP_&Open_P._v3;
	Where Channel not in ('OPAY','');
	Tables Channel*sign_to_FDD_time_bucket
	/  missing nofreq nopercent nocum nocol ;
Run;
*/

